<Program> ::= <DefinitionList> <Principal>

<DefinitionList> ::= <FunctionDef> <DefinitionList> | E

<FunctionDef> ::= "ruray" <IDENTIFICADOR_TOKEN> "(" <ParamListOpt> ")" <TypeOpt> <Block>

<Principal> ::= "ruray" "hatun_ruray" "(" ")" <Block>

<ParamListOpt> ::= <ParamList> | E
<ParamList> ::= <Param> <MoreParams>
<MoreParams> ::= "," <Param> <MoreParams> | E
<Param> ::= <Type> ":" <IDENTIFICADOR_TOKEN>

<Type> ::= "yupay" | "chiqi" | "chiqap" | "qillqa"
<TypeOpt> ::= <Type> | E

<Block> ::= "{" <Instrucciones> "}"
<Instrucciones> ::= <Instruccion> <Instrucciones> | E

<Instruccion> ::= <DeclaracionVariables>
               | <PrintStmt>
               | <Bucle>
               | <Estructura_If>
               | <Retorno>
               | <incrementos> ";"

<DeclaracionVariables> ::= "var" <ListaIdentificadores> <Type> <InicializacionOpt> ";"
                         | <ListaIdentificadores> ":=" <opciones> ";"
                         | <IDENTIFICADOR_TOKEN> ":=" <opciones> ";"

<ListaIdentificadores> ::= <IDENTIFICADOR_TOKEN> <MasIdentificadores>
<MasIdentificadores> ::= "," <IDENTIFICADOR_TOKEN> <MasIdentificadores> | E
<InicializacionOpt> ::= "=" <opciones> | E

<PrintStmt> ::= "imprimiy" "(" <ArgumentPrint> ")" ";"
<ArgumentPrint> ::= <opciones> <MasArgumentosPrint> | E
<MasArgumentosPrint> ::= "," <opciones> <MasArgumentosPrint> | E

<opciones> ::= <funcion> 
             | <comparacion> 
             | <IDENTIFICADOR_TOKEN> 
             | <datos> 
             | <operaciones_matematicas>
             | <incrementos>

<incrementos> ::= <IDENTIFICADOR_TOKEN> "++" | <IDENTIFICADOR_TOKEN> "--"

<operaciones_matematicas> ::= <termino> <expresion_tail>
<expresion_tail> ::= "+" <termino> <expresion_tail>
                   | "-" <termino> <expresion_tail>
                   | E
<termino> ::= <factor> <termino_tail>
<termino_tail> ::= "*" <factor> <termino_tail>
                 | "/" <factor> <termino_tail>
                 | "%" <factor> <termino_tail>
                 | E

<factor> ::= <unidad> | "-" <unidad>
<unidad> ::= <valor> | "(" <operaciones_matematicas> ")"
<valor> ::= <YUPAY_TOKEN> | <CHIQI_TOKEN> | <CHIQAP_TOKEN> | <QILLQA_TOKEN> | <IDENTIFICADOR_TOKEN> | <funcion>

<funcion> ::= <IDENTIFICADOR_TOKEN> "(" <argumentosFuncion> ")"
<argumentosFuncion> ::= <opciones> <MasArgumentosFuncion> | E
<MasArgumentosFuncion> ::= "," <opciones> <MasArgumentosFuncion> | E

<comparacion> ::= <logical_expression>
<logical_expression> ::= "mana" "(" <expression> ")"
                       | "(" <expression> ")"
                       | <expression>

<expression> ::= <compa> <expression_conti>
<expression_conti> ::= <opciones_operador_logico> <expression> | E
<compa> ::= <opciones> <opciones_operador_comparacion> <opciones>
          | <opciones> <opciones_operador_logico> <opciones>

<Bucle> ::= <for> | <while> | <bucle_infinito>
<for> ::= "para" "(" <LoopInitialization> ";" <opciones> ";" <LoopUpdate> ")" <Block>
<while> ::= "para" "(" <opciones> ")" <Block>
<bucle_infinito> ::= "para" <Block>

<LoopInitialization> ::= <IDENTIFICADOR_TOKEN> ":=" <asignacion_bucle>
<asignacion_bucle> ::= <funcion> | <IDENTIFICADOR_TOKEN> | <datos>
<LoopUpdate> ::= <operaciones_matematicas>

<Estructura_If> ::= "sichus" "(" <opciones> ")" <Block> <Else_opcional>
<Else_opcional> ::= "mana_sichus" <argumentosELseIf> <Block> | E
<argumentosELseIf> ::= "(" <opciones> ")" | E

<Retorno> ::= "kutipay" <IDENTIFICADOR_TOKEN> ";"
            | "kutipay" ";"
            | "kutipay" <datos> ";"
            | "pakiy" ";"

<datos> ::= <YUPAY_TOKEN> | <CHIQI_TOKEN> | <CHIQAP_TOKEN> | <QILLQA_TOKEN> | <operaciones_matematicas> | <IDENTIFICADOR_TOKEN>

<YUPAY_TOKEN> ::= "num"
<CHIQI_TOKEN> ::= "num" "." "num"
<CHIQAP_TOKEN> ::= "chiqaq" | "mana_chiqaq"
<QILLQA_TOKEN> ::= "texto"
<IDENTIFICADOR_TOKEN> ::= "id"

<OPERADOR_ASIGNACION> ::= ":="
<OPERADOR_LOGICO_UTAQ> ::= "utaq"
<OPERADOR_LOGICO_WAN> ::= "wan"
<OPERADOR_LOGICO_MANA> ::= "mana"
<opciones_operador_logico> ::= "wan" | "utaq"
<opciones_operador_comparacion> ::= "==" | "!=" | "<" | ">" | "<=" | ">="
